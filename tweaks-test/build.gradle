evaluationDependsOn(':tweaks')
evaluationDependsOn(':tweaks-compiler')

buildscript {
    repositories {
        mavenCentral()
    }
}

// As far as I can tell, we can't use the actual plugin because here because building the plugin happens
// after we evaluate the buildscript {} portion of this file. The shoemaker's kids
// go shoeless, I guess...

class LocalTweaksGradlePlugin implements Plugin<Project> {
    @Override
    void apply(final Project project) {
        project.afterEvaluate {
            if (project.android.hasProperty("libraryVariants")) {
                project.android.libraryVariants.all { variant ->
                    variant.javaCompile.options.compilerArgs += [
                            '-processor', 'com.mixpanel.android.compile.TweaksAnnotationProcessor'
                    ]
                }
            }

            if (project.android.hasProperty("testVariants")) {
                project.android.testVariants.all { variant ->
                    variant.javaCompile.options.compilerArgs += [
                            '-processor', 'com.mixpanel.android.compile.TweaksAnnotationProcessor'
                    ]
                }
            }

            if (project.android.hasProperty("applicationVariants")) {
                project.android.applicationVariants.all { variant ->
                    variant.javaCompile.options.compilerArgs += [
                            '-processor', 'com.mixpanel.android.compile.TweaksAnnotationProcessor'
                    ]
                }
            }
        }
    }
}

apply plugin: 'android-library'
apply plugin: LocalTweaksGradlePlugin

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
         minSdkVersion 9
        targetSdkVersion 21
        testApplicationId "com.mixpanel.android.mpmetrics"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"

        lintOptions {
            abortOnError true
            warningsAsErrors true
        }
    }
}

repositories {
    mavenLocal()
}

dependencies {
    compile 'com.mixpanel.android:mixpanel-android:4.5.4-SNAPSHOT@aar'
    compile project(':tweaks')
    provided project(':tweaks-compiler')
}